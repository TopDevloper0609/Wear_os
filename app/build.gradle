apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: "com.neenbedankt.android-apt"
apply plugin: 'com.siimkinks.sqlitemagic'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    notThere {
        file rootProject.file('debug_gradle.properties')
    }
    secrets {
        file rootProject.file('gradle.properties')
    }
}

android {
    signingConfigs {
        signing {
            keyAlias buildProperties.secrets['android_key_alias'].or(buildProperties.notThere['android_key_alias']).string
            keyPassword buildProperties.secrets['android_store_password'].or(buildProperties.notThere['android_store_password']).string
            storeFile file('fastaccess-key')
            storePassword buildProperties.secrets['android_store_password'].or(buildProperties.notThere['android_store_password']).string
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.fastaccess.github"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 111
        versionName "1.1.1"
        signingConfig signingConfigs.signing
        buildConfigString  "GITHUB_CLIENT_ID", buildProperties.secrets['github_client_id']
                .or(buildProperties.notThere['github_client_id']).string
        buildConfigString  "GITHUB_SECRET", buildProperties.secrets['github_secret']
                .or(buildProperties.notThere['github_secret']).string
        buildConfigString  "REDIRECT_URL", buildProperties.secrets['redirect_url']
                .or(buildProperties.notThere['redirect_url']).string
        buildConfigField "String",  "REST_URL", '"https://api.github.com/"'
        buildConfigField "int", "DB_VERSION", "2"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    productFlavors {
        debuggingApp {}
        productionApp {}
    }

    sourceSets {
        main {
            res.srcDirs = [
                    "src/main/res/",
                    "src/main/res/layouts/main_layouts",
                    "src/main/res/layouts/row_layouts",
                    "src/main/res/layouts/other_layouts",
                    "src/main/res/translations",
                    "src/main/res"
            ]
        }
    }

    lintOptions {
        abortOnError false
        htmlReport true
        xmlReport true
        disable 'InvalidPackage'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations {
        all*.exclude module: 'annotations'
    }
}

retrolambda {
    oldJdk System.getenv("JAVA7_HOME")
}

repositories {
    maven { url "https://clojars.org/repo/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "http://dl.bintray.com/amulyakhare/maven" }
    maven { url "https://jitpack.io" }
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

dependencies {
    ext {
        butterKnifeVersion = '8.5.1'
        icepickVersion = '3.2.0'
        lombokVersion = '1.12.6'
        supportVersion = "25.2.0"
        firebase = "10.2.0"
        thirtyinchVersion = '0.8.0-rc3'
        retrofit = '2.1.0'
    }
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:preference-v14:${supportVersion}"
    compile "com.android.support:customtabs:${supportVersion}"
    compile "com.google.firebase:firebase-ads:${firebase}"
    releaseCompile "com.google.firebase:firebase-crash:${firebase}"
    compile "net.grandcentrix.thirtyinch:thirtyinch:${thirtyinchVersion}"
    compile "net.grandcentrix.thirtyinch:thirtyinch-rx:${thirtyinchVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    compile 'com.firebase:firebase-jobdispatcher:0.5.2'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'cn.gavinliu.android.lib:ShapedImageView:0.8.3'
    compile "frankiesardo:icepick:${icepickVersion}"
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    compile 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.1-rc1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.annimon:stream:1.1.4'
    compile 'com.github.GrenderG:Toasty:1.1.3'
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.9.2'
    compile 'com.github.k0shk0sh:RetainedDateTimePickers:1.0.2'
    compile 'com.zzhoujay.markdown:markdown:1.0.4'
    compile 'com.github.daniel-stoneuk:material-about-library:1.8.1'
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "frankiesardo:icepick-processor:${icepickVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    apt "org.projectlombok:lombok:${lombokVersion}"
    apt "frankiesardo:icepick-processor:${icepickVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    provided "org.projectlombok:lombok:${lombokVersion}"
}

apply plugin: "com.google.gms.google-services"
